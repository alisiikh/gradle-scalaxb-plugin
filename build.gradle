buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.maiflai:gradle-scalatest:0.19'
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:3.7.0'
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.9"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}

group = projectGroup
version = projectVersion

apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'com.github.hierynomus.license'
apply plugin: 'com.diffplug.gradle.spotless'
apply plugin: 'com.github.maiflai.scalatest'

apply from: 'libs.gradle'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile libs.scala
    testCompile libs.scalaTest
    testCompile libs.pegdown
}

sourceSets {
    funcTest {
        scala.srcDir file('src/funcTest/scala')
        resources.srcDir file('src/funcTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task funcTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.funcTest.output.classesDirs
    classpath = sourceSets.funcTest.runtimeClasspath

    maxParallelForks = 1
}
check.dependsOn funcTest

gradlePlugin {
    testSourceSets sourceSets.funcTest
}

license {
    header = file('LICENSE')
    strictCheck = true
    skipExistingHeaders = false

    excludes(['**/generated/**/*.scala',
              '**/*.xml',
              '**/*.xsd'])

    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Oleksii Lisikh'
    ext.email = 'alisiikh@gmail.com'
}

spotless {
    scala {
        scalafmt('1.4.0').configFile('.scalafmt.conf')
    }
    format 'misc', {
        target '**/*.gradle', '**/*.properties'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

task licenseAndFormat {
    dependsOn 'licenseFormat', 'spotlessApply'
}

pluginBundle {
    mavenCoordinates {
        groupId = projectGroup
    }

    website = projectGithub
    vcsUrl = projectGithub
    description = pluginDescription
    tags = ['scalaxb', 'scala', 'xml', 'binding']

    plugins {
        scalaxbPlugin {
            id = 'com.github.alisiikh.scalaxb'
            displayName = pluginDescription
        }
    }
}
